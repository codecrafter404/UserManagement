prototype(Sandstorm.UserManagement:Component.ResetPassword.SetPassword) < prototype(Neos.Fusion:Component) {

    // all worked
    success = true
    // if set, the auth token was not found.
    tokenNotFound = false
    // if set, the token had a timeout.
    tokenTimeout = false

    indexLink = ''

    resetPasswordFlow = ''

    renderer = Neos.Fusion:Case {
        success {
            condition = ${success}
            renderer = Sandstorm.UserManagement:Component.ResetPassword.SetPassword.Success {
                resetPasswordFlow = ${props.resetPasswordFlow}
            }
        }
        tokenNotFound {
            condition = ${tokenNotFound}
            renderer = Sandstorm.UserManagement:Component.ResetPassword.SetPassword.LinkNotValid {
                indexLink = ${props.indexLink}
            }        }
        tokenTimeout {
            condition = ${tokenTimeout}
            renderer = Sandstorm.UserManagement:Component.ResetPassword.SetPassword.LinkExpired {
                indexLink = ${props.indexLink}
            }
        }
        default {
            condition = ${true}
            renderer = "General failure: invalid State"
        }
    }
}
prototype(Sandstorm.UserManagement:Component.ResetPassword.SetPassword.Success) < prototype(Neos.Fusion:Component) {

    resetPasswordFlow = ''

    renderer = afx`
        <Neos.Fusion.Form:Form
            form.target.package="Sandstorm.UserManagement"
            form.target.controller="ResetPassword"
            form.target.action="updatePassword"
            form.data.resetPasswordFlow={props.resetPasswordFlow}
        >
            <label>
                <Sandstorm.UserManagement:Component.FieldContainer
                    field.name="resetPasswordFlow[passwordDto][password]"
                    label="Neues Passwort"
                >
                    <Neos.Fusion.Form:Input  attributes.type="password" attributes.required="true"/>
                </Sandstorm.UserManagement:Component.FieldContainer>
            </label>
            <label>
                <Sandstorm.UserManagement:Component.FieldContainer
                    field.name="resetPasswordFlow[passwordDto][passwordConfirmation]"
                    label="Passwortbestätigung"
                >
                    <Neos.Fusion.Form:Input  attributes.type="password" attributes.required="true"/>
                </Sandstorm.UserManagement:Component.FieldContainer>
            </label>
            <Neos.Fusion.Form:Button class="button large primary">Passwort zurücksetzen</Neos.Fusion.Form:Button>
        </Neos.Fusion.Form:Form>
    `
}
prototype(Sandstorm.UserManagement:Component.ResetPassword.SetPassword.LinkExpired) < prototype(Neos.Fusion:Component) {

    indexLink = ''

    renderer = afx`
        <div class="callout alert">
            <p>Entschuldigung, dieser Link zur Passwort-Änderung ist nicht mehr gültig!</p>
        </div>
        <a href={props.indexLink}>Neuen Passwortlink abrufen</a>
    `
}
prototype(Sandstorm.UserManagement:Component.ResetPassword.SetPassword.LinkNotValid) < prototype(Neos.Fusion:Component) {

    indexLink = ''

    renderer = afx`
        <div class="callout alert">
            <p>Entschuldigung, dieser Link zur Passwort-Änderung ist nicht gültig!</p>
        </div>
        <a href={props.indexLink}>Neuen Passwortlink abrufen</a>
    `
}
